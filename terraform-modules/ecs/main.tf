#----- ECS --------
resource "aws_ecs_cluster" "cluster" {
  name = local.envname

  capacity_providers = ["FARGATE", aws_ecs_capacity_provider.prov1.name]

  default_capacity_provider_strategy {
    capacity_provider = aws_ecs_capacity_provider.prov1.name
    weight            = 100
  }

  tags = var.tags
}

resource "aws_ecs_capacity_provider" "prov1" {
  name = "prov1"

  auto_scaling_group_provider {
    auto_scaling_group_arn = module.asg.autoscaling_group_arn
  }

  tags = var.tags

  depends_on = [aws_iam_service_linked_role.ecs]
}

#----- ECS  Resources--------
data "aws_ami" "amazon_linux_ecs" {
  most_recent = true

  owners = ["amazon"]

  filter {
    name   = "name"
    values = ["amzn2-ami-ecs-hvm-*-x86_64-ebs"]
  }

  filter {
    name   = "owner-alias"
    values = ["amazon"]
  }
}

module "asg" {
  source  = "terraform-aws-modules/autoscaling/aws"
  version = "~> 4.0"

  name = local.envname

  # Launch configuration
  lt_name   = local.envname
  create_lt = true
  use_lt    = true

  image_id                 = data.aws_ami.amazon_linux_ecs.id
  key_name                 = var.key_name
  instance_type            = var.ecs_ec2_instance_type
  security_groups          = [aws_security_group.ecs_instance.id]
  iam_instance_profile_arn = aws_iam_instance_profile.ecs-instance.arn
  user_data                = data.template_file.user_data.rendered

  # Auto scaling group
  // asg_name                  = local.envname
  vpc_zone_identifier       = var.public_subnet_ids
  health_check_type         = "EC2"
  min_size                  = 0
  max_size                  = 2
  desired_capacity          = var.ecs_ec2_instance_count
  wait_for_capacity_timeout = 0

  tags = [
    {
      key                 = "Environment"
      value               = var.environment
      propagate_at_launch = true
    },
    {
      key                 = "Cluster"
      value               = local.envname
      propagate_at_launch = true
    },
  ]
}

data "template_file" "user_data" {
  template = file("${path.module}/templates/user-data.sh")

  vars = {
    cluster_name = local.envname
  }
}

resource "aws_security_group" "ecs_instance" {
  name        = "ecs_container_instance_${local.envname}"
  description = "Allow TLS inbound traffic"
  vpc_id      = var.vpc_id

  ingress {
    description = "All Internal traffic"
    from_port   = 0
    to_port     = 65535
    protocol    = "tcp"
    cidr_blocks = [var.vpc_cidr]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = { Name = "ecs_container_instance_${local.envname}" }
}
